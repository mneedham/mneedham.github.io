+++
draft = false
date="2017-02-22 23:28:23"
title="Neo4j: How do null values even work?"
tag=['neo4j']
category=['neo4j']
+++

<p>
Every now and then I find myself wanting to import a CSV file into <a href="https://neo4j.com/developer/">Neo4j</a> and I always get confused with how to handle the various null values that can lurk within.
</p>


<p>
Let's start with an example that doesn't have a CSV file in sight. Consider the following list and my attempt to only return null values:
</p>



~~~cypher

WITH [null, "null", "", "Mark"] AS values
UNWIND values AS value
WITH value WHERE value = null
RETURN value

(no changes, no records)
~~~

<p>
Hmm that's weird. I'd have expected that at least keep the first value in the collection. What about if we do the inverse? 
</p>



~~~cypher

WITH [null, "null", "", "Mark"] AS values
UNWIND values AS value
WITH value WHERE value <> null
RETURN value

(no changes, no records)
~~~

<p>
Still nothing! Let's try returning the output of our comparisons rather than filtering rows:
</p>



~~~cypher

WITH [null, "null", "", "Mark"] AS values
UNWIND values AS value
RETURN value = null AS outcome

╒═══════╤═════════╕
│"value"│"outcome"│
╞═══════╪═════════╡
│null   │null     │
├───────┼─────────┤
│"null" │null     │
├───────┼─────────┤
│""     │null     │
├───────┼─────────┤
│"Mark" │null     │
└───────┴─────────┘
~~~

<p>
Ok so that isn't what we expected. Everything has an 'outcome' of 'null'! What about if we want to check whether the value is the string "Mark"?
</p>



~~~cypher

WITH [null, "null", "", "Mark"] AS values
UNWIND values AS value
RETURN value = "Mark" AS outcome

╒═══════╤═════════╕
│"value"│"outcome"│
╞═══════╪═════════╡
│null   │null     │
├───────┼─────────┤
│"null" │false    │
├───────┼─────────┤
│""     │false    │
├───────┼─────────┤
│"Mark" │true     │
└───────┴─────────┘
~~~

<p>
From executing this query we learn that if one side of a comparison is null then the return value is always going to be null.
</p>


<p>
So how do we exclude a row if it's null? 
</p>


<p>
It turns out we have to use the 'is' keyword rather than using the equality operator. Let's see what that looks like:
</p>



~~~cypher

WITH [null, "null", "", "Mark"] AS values
UNWIND values AS value
WITH value WHERE value is null
RETURN value

╒═══════╕
│"value"│
╞═══════╡
│null   │
└───────┘
~~~

<p>
And the positive case:
</p>



~~~cypher

WITH [null, "null", "", "Mark"] AS values
UNWIND values AS value
WITH value WHERE value is not null
RETURN value

╒═══════╕
│"value"│
╞═══════╡
│"null" │
├───────┤
│""     │
├───────┤
│"Mark" │
└───────┘
~~~

<p>
What if we want to get rid of empty strings? 
</p>



~~~cypher

WITH [null, "null", "", "Mark"] AS values
UNWIND values AS value
WITH value WHERE value <> ""
RETURN value

╒═══════╕
│"value"│
╞═══════╡
│"null" │
├───────┤
│"Mark" │
└───────┘
~~~

<p>
Interestingly that also gets rid of the null value which I hadn't expected. But if we look for values matching the empty string:
</p>



~~~cypher

WITH [null, "null", "", "Mark"] AS values
UNWIND values AS value
WITH value WHERE value = ""
RETURN value

╒═══════╕
│"value"│
╞═══════╡
│""     │
└───────┘
~~~

<p>It's not there either! Hmm what's going on here:</p>



~~~cypher

WITH [null, "null", "", "Mark"] AS values
UNWIND values AS value
RETURN value, value = "" AS isEmpty, value <> "" AS isNotEmpty

╒═══════╤═════════╤════════════╕
│"value"│"isEmpty"│"isNotEmpty"│
╞═══════╪═════════╪════════════╡
│null   │null     │null        │
├───────┼─────────┼────────────┤
│"null" │false    │true        │
├───────┼─────────┼────────────┤
│""     │true     │false       │
├───────┼─────────┼────────────┤
│"Mark" │false    │true        │
└───────┴─────────┴────────────┘
~~~

<p>
null values seem to get filtered out for every type of equality match unless we explicitly check that a value 'is null'. 
</p>


<p>So how do we use this knowledge when we're parsing CSV files using Neo4j's <a href="https://neo4j.com/developer/guide-import-csv/">LOAD CSV</a> tool?</p>


<p>
Let's say we have a CSV file that looks like this:
</p>



~~~bash

$ cat nulls.csv
name,company
"Mark",
"Michael",""
"Will",null
"Ryan","Neo4j"
~~~

<p>
So none of the first three rows have a value for 'company'. I don't have any value at all, Michael has an empty string, and Will has a null value. Let's see how LOAD CSV interprets this:
</p>



~~~cypher

load csv with headers from "file:///nulls.csv" AS row
RETURN row

╒═════════════════════════════════╕
│"row"                            │
╞═════════════════════════════════╡
│{"name":"Mark","company":null}   │
├─────────────────────────────────┤
│{"name":"Michael","company":""}  │
├─────────────────────────────────┤
│{"name":"Will","company":"null"} │
├─────────────────────────────────┤
│{"name":"Ryan","company":"Neo4j"}│
└─────────────────────────────────┘
~~~

<p>
We've got the full sweep of all the combinations from above. We'd like to create a <cite>Person</cite> node for each row but only create a <cite>Company</cite> node and associated 'WORKS_FOR' relationshp if an actual company is defined - we don't want to create a null company.</p>


<p>So we only want to create a company node and 'WORKS_FOR' relationship for the Ryan row.
</p>


<p>The following query does the trick:</p>



~~~cypher

load csv with headers from "file:///nulls.csv" AS row
MERGE (p:Person {name: row.name})
WITH p, row
WHERE row.company <> "" AND row.company <> "null"
MERGE (c:Company {name: row.company})
MERGE (p)-[:WORKS_FOR]->(c)

Added 5 labels, created 5 nodes, set 5 properties, created 1 relationship, statement completed in 117 ms.
~~~

<p>And if we visualise what's been created:</p>


<div>

<img src="http://www.markhneedham.com/blog/wp-content/uploads/2017/02/graph-15.png" alt="Graph  15" title="Visualising the null graph" border="0" width="250"  />
</div>

<p>
Perfect. Perhaps this behaviour is obvious but it always trips me up so hopefully it'll be useful to someone else as well!
</p>


<p>
There's also a section on the Neo4j developer pages describing <a href="http://neo4j.com/docs/developer-manual/current/cypher/syntax/working-with-null/">even more null scenarios</a> that's worth checking out.
</p>

